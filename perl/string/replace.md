---
title: 文字列の置換／文字の置換
created: 2004-02-15
---

文字列の置換 (s/AAA/BBB/)
----

~~~ perl
$line =~ s/PATTERN/REPLACEMENT/[OPTION]
~~~

という構文で、文字列 `$line` に含まれる PATTERN というテキストを REPLACEMENT に置換し、`$line` に書き戻すことができます。
対象となる文字列が `$_` である場合は、`$line =~` の部分は省略することができます。

### s///のオプション

| オプション | 何の略称か | 意味 |
| ---- | ---- | ---- |
| e | eval | 置換用の文字列を実行時に評価する |
| g | global | マッチするものを全て置換し、置換数を返す |
| i | ignore |  大文字、小文字を無視 |
| m | multiple | 文字列を複数行として扱う (`\n` を行の区切りとみなす) |
| o | once | パターン部分のコンパイルを一度だけする |
| s | single | 文字列を単一行として扱う (`\n` を行の区切りとみなさない) |
| x | extended | 空白類の無視 |

#### 例: 大なり記号と小なり記号をエスケープする

~~~ perl
while (<>) {
    s/</&lt;/g;
    s/>/&gt;/g;
    print;
}
~~~


文字単位の置換 (tr/AAA/BBB/)
----

~~~ perl
$line =~ tr/SEARCHLIST/REPLACEMENTLIST/[OPTION]
~~~

という構文で、文字列 `$line` に含まれる文字群 (SEARCHLIST) を REPLACEMENTLIST に置換して `$line` に書き戻すことができます。
対象となる文字列が `$_` である場合は、`$line =~` の部分は省略することができます。

### tr///のオプション

| オプション | 何の略称か | 意味 |
| ---- | ---- | ---- |
| c | complement | 検索リストに文字の補集合を使う |
| d | delete | 検索リストにあり変換リストにないものを全て消す |
| s | squeeze | 変換して同じ文字になった場合１文字に縮める |


#### 例: A を X に、B を Y に、C を Z に置換する

~~~ perl
$line =~ tr/ABC/XYZ/;
~~~


### 最後の文字の繰り返し

REPLACEMENTLIST が SEARCHLIST よりも短い場合は、REPLACEMENTLIST の最後の文字が繰り返し使用されます。

~~~ perl
tr/a-f/abc/;   # tr/a-f/abcccc/; とみなされる
~~~

上の場合は、

~~~
a → a
b → b
c → c
d → c
e → c
f → c
~~~

とそれぞれの文字が置換されることになります。
ただし、オプションに `d` を指定した場合は、対応文字がない文字は削除されるという振る舞いに変化します。

~~~ perl
tr/a-f/abc/d;  # 'd' 'e' 'f' は消える
~~~


### 変換リストが空の時

変換リストが空の時は、置換リストと同じ文字列が使われます。

~~~ perl
tr/A-Z//;  # tr/A-Z/A-Z/;
~~~

つまり上のようにした場合は、`$_` に何の変化も起こりません。
A ～ Z の文字を消したい時は、`d` オプションを指定しなければいけません。


### 戻り値を利用して文字数を数える

~~~ perl
$count = tr/0-9//;    # 数字の文字数を数える
$count = tr/0-9//d;   # 数字を消す
$count = tr/0-9//c;   # 数字以外の文字数を数える
$count = tr/0-9//cd;  # 数字以外を消す
~~~


### s オプションを利用する

以下の例は `s` オプションを使って、アルファベット以外の連続した文字を `\n` に置き換えます。
これで英単語をリストアップできます。

~~~ perl
while (<>) {
    tr/A-Za-z/\n/cs;  # (\n)英単語\n英単語\n英単語\n ...
    s/^\n//;          # 先頭に \n があれば削除
    print;
}
~~~

